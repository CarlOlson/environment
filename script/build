#!/usr/bin/env fish

cd (dirname (status --current-filename))/..

set --local --export Distro Unknown
if uname -a | grep Ubuntu
    set Distro Ubuntu
end

if uname -a | grep Darwin
    set -x PATH $HOME/.nix-profile/bin $PATH
end

if type -q awesome && not awesome --check -c awesome/rc.lua
    echo "Bad awesome config!"
    exit 1
end

if test -f $HOME/.emacs.d/init.el
    rm $HOME/.emacs.d/init.el
end

if test -f $HOME/.emacs.d/init.elc
    rm $HOME/.emacs.d/init.elc
end

mkdir -p $HOME/.emacs.d
ln -s $PWD/elisp/init.el $HOME/.emacs.d/init.el

if not command -sq home-manager
    nix-channel --add https://github.com/nix-community/home-manager/archive/master.tar.gz home-manager
    nix-channel --update
    nix-shell '<home-manager>' -A install
end

if home-manager switch -f home.nix
    if type -q awesome-client
        awesome-client "awesome.restart()"
    end

    source fish/config.fish

    echo "Success. Now restart emacs."
else
    exit 1
end

if ! string match -qr fish "$SHELL $BOOTSTRAP_SHELL"
    echo "Not using fish shell, try the following:"
    echo -e "\tsudo chsh --shell $HOME/.nix-profile/bin/fish $USER"
end
